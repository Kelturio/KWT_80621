FUNCTION_BLOCK "Hmi"
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Searinox
VERSION : 0.1
   VAR_INPUT 
      iFan1 : "Fan";
      iFan2 : "Fan";
      iPumpPlant1 : "Pump";
      iPumpPlant2 : "Pump";
      iPumpCoolWater1 : "Pump";
      iPumpCoolWater2 : "Pump";
      iPumpFurnace1 : "Pump";
      iPumpFurnace2 : "Pump";
      iCt1RepairSwitchOff : Bool;
      iCt2RepairSwitchOff : Bool;
      iCt1SprayPipeOpen : Bool;
      iCt1SprayPipeIsOpen : Bool;
      iCt1SprayPipeErr : Bool;
      iCt2SprayPipeOpen : Bool;
      iCt2SprayPipeIsOpen : Bool;
      iCt2SprayPipeErr : Bool;
      iAutoMode : Bool;
      iSurgeTankRefill : Bool;
   END_VAR

   VAR 
      sPumpPlant1 : Int;
      sPumpPlant2 : Int;
      sPumpCoolWater1 : Int;
      sPumpCoolWater2 : Int;
      sPumpFurnace1 : Int;
      sPumpFurnace2 : Int;
      sAnimationFrame : Int := 1;
      sCt1Fan : Int;
      sCt2Fan : Int;
      IEC_TMR_0 {OriginalPartName := 'IEC_TIMER'; LibVersion := '1.0'} : IEC_TIMER;
      sTimer0Start : Bool := true;
      sTimer0Pt : Time := T#100ms;
      sTimer0Q : Bool;
      sCt1SprayPipe : Int;
      sCt2SprayPipe : Int;
      sAutoMode : Bool := false;
      IEC_TMR_1 {OriginalPartName := 'IEC_TIMER'; LibVersion := '1.0'} : IEC_TIMER;
      sAutoModePt : Time := T#5s;
      R_TRIG {OriginalPartName := 'R_TRIG_1200'; LibVersion := '1.0'} : R_TRIG;
      sSurgeTankRefill : Int;
   END_VAR

   VAR_TEMP 
      tAutoModeTimer : Bool;
      tAutoModeTrigger : Bool;
   END_VAR


BEGIN
	#sPumpPlant1 := "HmiGetStatusColorPump"(iPump := #iPumpPlant1);
	#sPumpPlant2 := "HmiGetStatusColorPump"(iPump := #iPumpPlant2);
	
	#sPumpCoolWater1 := "HmiGetStatusColorPump"(iPump := #iPumpCoolWater1);
	#sPumpCoolWater2 := "HmiGetStatusColorPump"(iPump := #iPumpCoolWater2);
	
	#sPumpFurnace1 := "HmiGetStatusColorPump"(iPump := #iPumpFurnace1);
	#sPumpFurnace2 := "HmiGetStatusColorPump"(iPump := #iPumpFurnace2);
	
	#sCt1Fan := "HmiGetStatusColorFan"(iFan:=#iFan1,
	                                   iAnimationFrame := #sAnimationFrame);
	#sCt2Fan := "HmiGetStatusColorFan"(iFan:=#iFan2,
	                                   iAnimationFrame := #sAnimationFrame);
	
	#sCt1SprayPipe := "HmiGetStatusColorValve"(iValveQ := #iCt1SprayPipeOpen,
	                                           iValveOpen := #iCt1SprayPipeIsOpen,
	                                           iValveErr := #iCt1SprayPipeErr);
	#sCt2SprayPipe := "HmiGetStatusColorValve"(iValveQ := #iCt2SprayPipeOpen,
	                                           iValveOpen := #iCt2SprayPipeIsOpen,
	                                           iValveErr := #iCt2SprayPipeErr);
	
	#sSurgeTankRefill := "HmiGetStatusColorValve"(iValveQ := #iSurgeTankRefill,
	                                              iValveOpen := #iSurgeTankRefill,
	                                              iValveErr := FALSE);
	
	#IEC_TMR_0.TON(IN := #sTimer0Start,
	               PT := #sTimer0Pt,
	               Q => #sTimer0Q);
	
	IF #sTimer0Q THEN
	    RESET_TIMER(#IEC_TMR_0);
	    #sAnimationFrame += 1;
	    IF #sAnimationFrame = 5 THEN
	        #sAnimationFrame := 1;
	    END_IF;
	END_IF;
	
	#IEC_TMR_1.TON(IN := #iAutoMode,
	               PT := #sAutoModePt,
	               Q => #tAutoModeTimer);
	
	#R_TRIG(CLK := #tAutoModeTimer,
	        Q => #tAutoModeTrigger);
	
	IF #tAutoModeTrigger THEN
	    #sAutoMode := NOT #sAutoMode;
	END_IF;
END_FUNCTION_BLOCK

